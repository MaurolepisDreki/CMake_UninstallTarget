# CMake uninstall routine

if( NOT EXISTS "@CMAKE_BINARY_DIR@/install_manifest.txt" )
	message( FATAL_ERROR "FILE NOT FOUND : install_manifest.txt" )
endif()

file( READ "@CMAKE_BINARY_DIR@/install_manifest.txt" files )
string( REGEX REPLACE "\n" ";" files "${files}" )
foreach( file ${files} )
	# Remove File
	message( STATUS "Uninstalling $ENV{DESTDIR}${file}" )
	if( EXISTS "$ENV{DESTDIR}${file}" )
		execute_process(
			COMMAND "@CMAKE_COMMAND@" -E rm "$ENV{DESTDIR}${file}"
			RESULT_VARIABLE rm_retval
			ERROR_VARIABLE rm_error
		)

		if( NOT "${rm_retval}" STREQUAL 0 )
			message( FATAL_ERROR "${rm_error}" )
		endif()
	else()
		message( NOTICE "FILE NOT FOUND : $ENV{DESTDIR}${file}" )
	endif()

	#Trim Directory Tree
	while( NOT FALSE )
		string( REGEX REPLACE "^(.*)/[^/]*$" "\\1" file ${file} )
		if( "${file}" STREQUAL "" )
			break()
		endif()
		file( GLOB filelist LIST_DIRECTORIES true "${file}/*" )
		if( "${filelist}" STREQUAL "" )
			message( CHECK_START "Triming Directory: ${file}" )
			file( REMOVE_RECURSE "${file}" )
			if( EXISTS "${file}" )
				message( CHECK_FAIL "FAILED" )
			else()
				message( CHECK_PASS "DONE" )
			endif()
		endif()
	endwhile()
endforeach()

